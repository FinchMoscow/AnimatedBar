import com.hierynomus.gradle.license.tasks.LicenseCheck
import com.hierynomus.gradle.license.tasks.LicenseFormat

// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "com.github.hierynomus.license"

buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath 'org.jlleitschuh.gradle:ktlint-gradle:7.4.0'
        classpath 'gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.15.0'

        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1'

        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "com.github.hierynomus.license"
}

allprojects {
    repositories {
        google()
        jcenter()

    }

    // Licensing

    license {
        header = rootProject.file('license_header.md')
        strictCheck true
    }

    def ktSource = fileTree(dir: "src/main/java").include("**/*.kt")

    task licenseCheckForKotlin(type: LicenseCheck) { source = ktSource }
    project.tasks.getByName('license').dependsOn licenseCheckForKotlin

    task licenseFormatForKotlin(type: LicenseFormat) { source = ktSource }
    licenseFormat.dependsOn licenseFormatForKotlin
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
