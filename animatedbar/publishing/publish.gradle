apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'com.jfrog.bintray'

def GROUP_ID = "fm.finch.android"
def ARTIFACT_ID = "animatedbar"

def BINTRAY_REPOSITORY = "AnimatedBar"
def BINTRAY_NAME = "fm.finch.android.animatedbar"

def WEBSITE_URL = "https://gitlab.com/akishindev/animatedbar"
def VCS_URL = "https://gitlab.com/akishindev/animatedbar.git"
def ISSUES_URL = "https://gitlab.com/akishindev/animatedbar/issues"

def LIBRARY_VERSION_NAME = "1.0.1"

group = GROUP_ID
version = LIBRARY_VERSION_NAME

task sourcesJar(type: Jar) {
    classifier = 'sources'
    duplicatesStrategy = "exclude"
    from android.sourceSets.main.java.source
}

publishing {
    publications {
        Production(MavenPublication) {
            groupId = GROUP_ID
            artifactId = ARTIFACT_ID
            version = LIBRARY_VERSION_NAME

            from components.android
            artifact sourcesJar
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = BINTRAY_REPOSITORY
        name = BINTRAY_NAME

        publish = true
        override = true

        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = VCS_URL
        websiteUrl = WEBSITE_URL
        issueTrackerUrl = ISSUES_URL
        dryRun = false

        version {
            name = LIBRARY_VERSION_NAME
            vcsTag = LIBRARY_VERSION_NAME
            released = new Date()
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }

        publications = ['Production']
    }
}

bintrayUpload.doFirst {
    publications = publishing.publications.collect {
        it.name
    }
}

bintrayUpload.dependsOn publishToMavenLocal
